plugins {
    id 'java'
    id 'application'
}

group = 'com.ever2e'
version = '0.1.0'

java {
    sourceCompatibility = JavaVersion.VERSION_25
    targetCompatibility = JavaVersion.VERSION_25
}

layout.buildDirectory.set(file('target'))

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
            exclude 'test/**'
        }
    }
    test {
        java {
            srcDirs = ['src/test']
        }
    }
}

application {
    mainClass = 'core.emulator.machine.machine8.Emulator8Coordinator'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release = 25
}

tasks.named('run', JavaExec) {
    workingDir = projectDir
}

tasks.register('runHeadless', JavaExec) {
    group = 'application'
    description = 'Run Emulator8Coordinator with headless AWT mode enabled.'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = application.mainClass
    workingDir = projectDir
    jvmArgs '-Djava.awt.headless=true'
}
